buildscript {
    repositories {
        // mavenCentral is currently not needed but could be useful in the future if we decide for example to use Jafama
        // mavenCentral()
        maven {
            name 'Forge'
            url 'https://maven.minecraftforge.net'
        }
    }
    dependencies {
        classpath 'com.anatawa12.forge:ForgeGradle:2.3-1.0.7'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

// Add version to the Jar name
version project.mod_version

minecraft {
    version = "1.12.2-14.23.0.2491"
    mappings = "snapshot_20170624"

    runDir = "run"

    def args = '-Dfml.coreMods.load=com.vicmatskiv.weaponlib.core.WeaponlibCorePlugin'
    clientJvmArgs.add(args)
    serverJvmArgs.add(args)

    replace('@VERSION@', project.mod_version)
    replaceIn('ModernWarfareMod.java')
}


processResources {
    duplicatesStrategy = 'include'

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.mod_version
    }

    from ('src/main/java') {
        include '**/*.png'

        include '**/*.json'

        include '**/*.vsh'

        include '**/*.fsh'
    }
}

sourceJar {
    // Weird should not be needed at all, but something in Weapon Lib seems to be duplicated or something.
    // Doing this works just fine so it will be good enough for now.
    // For future reference or if anyone want to take a guess:
    // Execution failed for task ':sourceJar'.
    // > Entry com/vicmatskiv/weaponlib/model/CameraModel.java is a duplicate but no duplicate handling strategy has been set. Please refer to https://docs.gradle.org/7.6/dsl/org.gradle.api.tasks.Copy.html#org.gradle.api.tasks.Copy:duplicatesStrategy for details.
    duplicatesStrategy = 'include'
}

jar {
    manifest.attributes([
            'ModSide': 'BOTH',
            'FMLCorePlugin': 'com.vicmatskiv.weaponlib.core.WeaponlibCorePlugin',
            'FMLCorePluginContainsFMLMod': 'true',
            'ForceLoadAsMod': 'true'
    ])
}
